#!/bin/gawk -f
#
# Сливает несколько файлов контекстов SELinux в один.
# Порядок строк в файлах, и взаимный порядок строк файлов не меняется.
# Если один и тот же контекст перечислен несколько раз, побеждает более поздний.
#
# Запуск: merge-contexts file_context1 file_context2 ... > merged_context
#

BEGIN {
	count=0
}

{
	if (NF == 2) {
		ft = ""
		fc = $2
	} else {
		ft = $2
		fc = $3
	}

	if ($1 in secon) {
		if (secon[$1][1] != fc) {
			print "Контекст "$1" определён несколько раз!" > "/dev/stderr"
			print "	-> было "secon[$1][1]", стало "fc > "/dev/stderr"
		}
		idx = secon[$1][2]
	} else {
		idx = count
		count += 1
		secon_idx[idx] = $1
	}

	secon[$1][0] = ft
	secon[$1][1] = fc
	secon[$1][2] = idx
}

END {
	for (x = 0; x < count; x++) {
		idx = secon_idx[x]
		ntabs = int ((80 - length (idx) + 7) / 8)
		if (ntabs <= 0)
			ntabs = 1;
		line = idx
		while (ntabs > 0) {
			line = line"\t"
			ntabs--;
		}
		if (secon[idx][0] != "")
			line = line""secon[idx][0]"\t"
		else
			line = line"\t"
		line = line""secon[idx][1]
		print line
	}
}
